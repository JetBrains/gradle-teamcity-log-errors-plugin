apply plugin: 'groovy'

repositories {
    mavenCentral()
    maven { url 'https://download.jetbrains.com/teamcity-repository' }
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.jetbrains.teamcity:common-api:9.1.6'

    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testRuntime files(createClasspathManifest)
}
configurations.testRuntime {
    exclude module: 'slf4j-log4j12'
}

compileGroovy {
    targetCompatibility = 1.6
}

apply plugin: 'maven-publish'
publishing {
    repositories {
        maven {
            name 'dev'
            url "$buildDir/repo"
        }

        // saved in 'gradle.properties' file
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                name 'release'
                url 'http://download.jetbrains.com/teamcity-repository'
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java

            groupId 'org.jetbrains.teamcity.gradle'
            artifactId 'log-errors'
            version '1.2-SNAPSHOT'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
